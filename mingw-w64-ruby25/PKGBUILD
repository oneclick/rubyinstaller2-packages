_realname=ruby
pkgbase=mingw-w64-ruby25
pkgname="${MINGW_PACKAGE_PREFIX}-ruby25"
# version used by MRI
pkgver2=2.5.9
# version used by pacman
pkgver="${pkgver2/-/.}"
pkgrel=2
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=('any')
url="https://www.ruby-lang.org/en"
license=("BSD, custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gdbm>=1.17"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-libyaml"
         "${MINGW_PACKAGE_PREFIX}-openssl>=1.1"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("https://cache.ruby-lang.org/pub/ruby/${pkgver2%.*}/${_realname}-${pkgver2}.tar.xz"
        0001-win32ole-separate-global-variable-declarations-and-d.patch
        0002-use-gnu-printf.patch
        0003-Fix-path-checks-for-case-insensitive-filesystem.patch
        ruby.ico
        rubyw.ico)

# To update SHA256 hashs run: ridk exec sh -c "updpkgsums PKGBUILD"

sha256sums=('a87f2fa901408cc77652c1a55ff976695bbe54830ff240e370039eca14b358f0'
            'ab6784915e81f731aed8498320fe468767be6e5b3fa3586b997d20fb09aa5154'
            '9770717e1f9cb981526496148fe5c5d048acfba78c8c478c0ccf83d8f51fed79'
            'c8f6c493c480e7eb00da39b6d1cf8634b470ce58f3af1ce7891a6d6a7967435f'
            '6c80ba2ca49840e387a08b7fedc6e7acd298ac2ec853155209efb2af20397b22'
            'c30fb04ac0e88ba634daaa811a81ea5a75027646617b895acd14df9518a4a55a')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver2}
  patch -p1 -i ${srcdir}/0001-win32ole-separate-global-variable-declarations-and-d.patch
  patch -p1 -i ${srcdir}/0002-use-gnu-printf.patch
  patch -p1 -i ${srcdir}/0003-Fix-path-checks-for-case-insensitive-filesystem.patch
}

build() {

  CPPFLAGS+=" -DFD_SETSIZE=2048"
  CFLAGS="-O3 -fno-fast-math -fstack-protector-strong"
  LDFLAGS="-pipe -s -fstack-protector-strong"

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  cp ${srcdir}/*.ico .

  ../${_realname}-${pkgver2}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-out-ext=readline,pty,syslog

  make showflags main
  echo "Generate RI and HTML documentation"
  # Make rdoc outputs so many lines -> print dots only
  make rdoc html 2>&1 | ./miniruby -ne "STDERR.print '.'"
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  echo $PATH
  make -l "TESTOPTS=-q" btest test-basic
  # TODO: Full tests fail currently
  # make -l "TESTOPTS=-q" test-all || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -f GNUMakefile DESTDIR="${pkgdir}" install
  for script in {erb,gem,irb,rdoc,ri}; do
    install ${srcdir}/ruby-${pkgver2}/bin/${script} \
      ${pkgdir}${MINGW_PREFIX}/bin/
  done

  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/doc/ruby/
  cp -r .ext/html \
    ${pkgdir}${MINGW_PREFIX}/share/doc/ruby/
}
