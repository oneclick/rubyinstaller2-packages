_realname=ruby
pkgbase=mingw-w64-ruby25
pkgname="${MINGW_PACKAGE_PREFIX}-ruby25"
pkgver=2.5.0.r20170617
pkgver() {
  echo 2.5.0.r`date +%Y%m%d`
}
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=('any')
url="https://www.ruby-lang.org/en"
license=("BSD, custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gdbm<1.11"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-libyaml"
         "${MINGW_PACKAGE_PREFIX}-openssl>=1.1"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("https://cache.ruby-lang.org/pub/ruby/snapshot.tar.xz"
        0001-Fix-overflow-detection-for-LLP64-arch.patch
        0002-use-gnu-printf.patch
        0004-exclude-changelog-from-rdoc.patch
        ruby.ico
        rubyw.ico)
noextract=("snapshot.tar.xz")

# To update SHA256 hashs run: ridk exec sh -c "updpkgsums PKGBUILD.erb"

sha256sums=('SKIP'
            '1bda935d3a7a4d706104416c2beb85363861c3bea22079567c8204f6db077098'
            '578bd0830fe96efc7656c732ec46b0658fc436a7a30d8945cf3b8240797809f0'
            '389236bdca5f283adde9b4b56febcfc25e228716e06a0edc21ea8fd7f27b75fa'
            '6c80ba2ca49840e387a08b7fedc6e7acd298ac2ec853155209efb2af20397b22'
            'c30fb04ac0e88ba634daaa811a81ea5a75027646617b895acd14df9518a4a55a')

prepare() {
  bsdtar -xf snapshot.tar.xz || true
  cd ${srcdir}/snapshot
  patch -p1 -i ${srcdir}/0001-Fix-overflow-detection-for-LLP64-arch.patch
  patch -p1 -i ${srcdir}/0002-use-gnu-printf.patch
  patch -p1 -i ${srcdir}/0004-exclude-changelog-from-rdoc.patch

  autoreconf -fi
}

build() {

  CPPFLAGS+=" -DFD_SETSIZE=2048"

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  cp ${srcdir}/*.ico .

  ../snapshot/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-out-ext=readline,pty,syslog

  make showflags main
  echo "Generate HTML documentation"
  # Make rdoc outputs so many lines -> print dots only
  make html 2>&1 | ./miniruby -ne "STDERR.print '.'"
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  echo $PATH
  make -l "TESTOPTS=-q" btest test-basic
  # TODO: Full tests fail currently
  # make -l "TESTOPTS=-q" test-all || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -f GNUMakefile DESTDIR="${pkgdir}" install-nodoc
  for script in {erb,gem,irb,rdoc,ri}; do
    install ${srcdir}/snapshot/bin/${script} \
      ${pkgdir}${MINGW_PREFIX}/bin/
  done

  mkdir -p ${pkgdir}${MINGW_PREFIX}/share/doc/ruby/
  cp -r .ext/html \
    ${pkgdir}${MINGW_PREFIX}/share/doc/ruby/
}
