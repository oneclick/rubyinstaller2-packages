_realname=ruby
pkgbase=mingw-w64-ruby32
pkgname="${MINGW_PACKAGE_PREFIX}-ruby32"
# version used by MRI
pkgver2=3.2.9
# version used by pacman
pkgver="${pkgver2/-/.}"
pkgrel=1
pkgdesc="An object-oriented language for quick and easy programming (mingw-w64)"
arch=('any')
mingw_arch=('mingw32' 'ucrt64')
url="https://www.ruby-lang.org/en"
license=("BSD, custom")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc" "${MINGW_PACKAGE_PREFIX}-pkg-config")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gmp"
         "${MINGW_PACKAGE_PREFIX}-libffi"
         "${MINGW_PACKAGE_PREFIX}-libyaml"
         "${MINGW_PACKAGE_PREFIX}-openssl>=3.0.0"
         "${MINGW_PACKAGE_PREFIX}-zlib")
options=('staticlibs' 'strip')
source=("https://cache.ruby-lang.org/pub/ruby/${pkgver2%.*}/${_realname}-${pkgver2}.tar.xz"
        0001-Add-C-ext-win32-dll_directory-as-an-alternative-to-f.patch
        0002-Don-t-add-an-exe-file-manifest.patch
        0003-Use-autoconf-2.72.patch
        0006-Windows-Prefer-USERPROFILE-over-HOMEPATH-on-startup-.patch
        0009-Win32-Return-registry-strings-as-UTF-8.patch
        0010-Mingw32-Use-ATOMIC_PTR-functions-to-avoid-compiler-w.patch
        0014-ssl-remove-OpenSSL-X509-V_FLAG_CRL_CHECK_ALL-from-th.patch
        ruby.ico
        rubyw.ico)

# To update SHA256 hashs run: ridk exec sh -c "updpkgsums PKGBUILD"

sha256sums=('cf6699d0084c588e7944d92e1a8edda28b1cc3ee471a1f0aebb4b3d32c9753b2'
            'a50c81ab8c178689485257f8eaa70bd8184b878a798780084668f838912e2832'
            'da079dbfdc3d4e1b976ed15d58244ed6e0df201ec829bead578fb4a7177b9cab'
            'f26122cca5e289801c0dde1f54643a960246a69691c6c8b9bccb27e536344f69'
            '7bba11585dc597281e1c616f00a482016d57e5fb983f02fb05ed6260a9858ffd'
            '5f84680f8cbce37b617f132e09009c111c78044d92378562456d8814db449346'
            '5947650b4c503cbc9fc176479b70e18e6d8ca2b21ddcf3763ee97972140420f7'
            '2e761b66d4ffc89e3bd3bf92ae2b102a8634ae47871b81f4c9e8103af62f8d00'
            '6c80ba2ca49840e387a08b7fedc6e7acd298ac2ec853155209efb2af20397b22'
            'c30fb04ac0e88ba634daaa811a81ea5a75027646617b895acd14df9518a4a55a')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver2}
  rm -rf ext/win32/dll_directory
  patch -p1 -i ${srcdir}/0001-Add-C-ext-win32-dll_directory-as-an-alternative-to-f.patch
  patch -p1 -i ${srcdir}/0002-Don-t-add-an-exe-file-manifest.patch
  patch -p1 -i ${srcdir}/0003-Use-autoconf-2.72.patch
  patch -p1 -i ${srcdir}/0006-Windows-Prefer-USERPROFILE-over-HOMEPATH-on-startup-.patch -l
  patch -p1 -i ${srcdir}/0009-Win32-Return-registry-strings-as-UTF-8.patch
  patch -p1 -i ${srcdir}/0010-Mingw32-Use-ATOMIC_PTR-functions-to-avoid-compiler-w.patch
  patch -p1 -i ${srcdir}/0014-ssl-remove-OpenSSL-X509-V_FLAG_CRL_CHECK_ALL-from-th.patch
  autoreconf -fi
}

build() {

  CPPFLAGS+=" -DFD_SETSIZE=2048"
  CFLAGS="-O3 -fno-fast-math -fstack-protector-strong"
  LDFLAGS="-pipe -s -fstack-protector-strong"

  [[ -d "${srcdir}/build-${MINGW_CHOST}" ]] && rm -rf "${srcdir}/build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}" && cd "${srcdir}/build-${MINGW_CHOST}"

  cp ${srcdir}/*.ico .

  ../${_realname}-${pkgver2}/configure \
    --prefix=${MINGW_PREFIX} \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --with-out-ext=readline,pty,syslog

  make showflags main
  echo "Generate RI and HTML documentation"
  # Make rdoc outputs so many lines -> print dots only
  make rdoc html 2>&1 | ./miniruby -ne "STDERR.print '.'"
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  echo $PATH
  make -l "TESTOPTS=-q" btest test-basic
  # TODO: Full tests fail currently
  # make -l "TESTOPTS=-q" test-all || true
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make -f GNUMakefile DESTDIR="${pkgdir}" install
  for script in gem; do
    install ${srcdir}/ruby-${pkgver2}/bin/${script} \
      ${pkgdir}${MINGW_PREFIX}/bin/
  done
}

